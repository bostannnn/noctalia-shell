pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Io
import qs.Commons
import qs.Services.UI

Singleton {
  id: root

  readonly property string colorsApplyScript: Quickshell.shellDir + '/Bin/colors-apply.sh'
  
  // Debounce timer to prevent rapid-fire generation
  property bool generationPending: false
  property string pendingWallpaper: ""
  
  Timer {
    id: generateDebounce
    interval: 500
    onTriggered: {
      root.generationPending = false
      if (root.pendingWallpaper !== "") {
        root.doGenerateFromWallpaper(root.pendingWallpaper)
        root.pendingWallpaper = ""
      } else {
        root.doGenerate()
      }
    }
  }

  Connections {
    target: WallpaperService

    // When the wallpaper changes, regenerate with Matugen if necessary
    function onWallpaperChanged(screenName, path) {
      if (screenName === Screen.name && Settings.data.colorSchemes.useWallpaperColors) {
        // Debounce wallpaper-triggered generation - always restart timer
        root.pendingWallpaper = path
        root.generationPending = true
        generateDebounce.restart()
      }
    }
  }

  Connections {
    target: Settings.data.colorSchemes
    function onDarkModeChanged() {
      Logger.d("AppThemeService", "Detected dark mode change");
      generate();
    }
  }

  // Create default hyprland config if template enabled but file missing
  Process {
    id: hyprlandInitProc
    command: ["sh", "-c", `
      FILE="$HOME/.config/hypr/noctalia.conf"
      if [ ! -f "$FILE" ]; then
        mkdir -p "$HOME/.config/hypr"
        cat > "$FILE" << 'HYPREOF'
# Noctalia Hyprland Theme - Auto-generated by matugen
# This file will be populated when you change wallpapers

$noctalia_primary = rgb(bd93f9)
$noctalia_secondary = rgb(ff79c6)
$noctalia_tertiary = rgb(8be9fd)
$noctalia_surface = rgb(282a36)
$noctalia_surface_variant = rgb(44475a)
$noctalia_outline = rgb(6272a4)
$noctalia_error = rgb(ff5555)
$noctalia_active_border1 = rgba(bd93f9ee)
$noctalia_active_border2 = rgba(ff79c6ee)
$noctalia_active_border3 = rgba(8be9fdee)
$noctalia_active_border4 = rgba(50fa7bee)
$noctalia_inactive_border = rgba(44475aaa)
$noctalia_bg = rgb(282a36)
$noctalia_bg_dim = rgb(21222c)
$noctalia_text = rgb(f8f8f2)
$noctalia_text_secondary = rgb(6272a4)

general {
    col.active_border = $noctalia_active_border1 $noctalia_active_border2 $noctalia_active_border3 $noctalia_active_border4 45deg
    col.inactive_border = $noctalia_inactive_border
}

group {
    col.border_active = $noctalia_active_border1 $noctalia_active_border2 $noctalia_active_border3 45deg
    col.border_inactive = $noctalia_inactive_border
    
    groupbar {
        col.active = $noctalia_primary
        col.inactive = $noctalia_surface_variant
    }
}
HYPREOF
        echo "Created default noctalia.conf"
      fi
    `]
  }

  // PUBLIC FUNCTIONS
  function init() {
    Logger.i("AppThemeService", "Service started");
    
    // Always create default hyprland config if it doesn't exist
    // This prevents "source globbing error" in hyprland.conf
    hyprlandInitProc.running = true;
  }

  function generate() {
    // Debounce to prevent rapid-fire generation
    root.pendingWallpaper = ""
    if (!generationPending) {
      generationPending = true
      generateDebounce.restart()
    }
  }
  
  function doGenerate() {
    if (Settings.data.colorSchemes.useWallpaperColors) {
      doGenerateFromWallpaper("");
    } else {
      // applyScheme will trigger template generation via schemeReader.onLoaded
      ColorSchemeService.applyScheme(Settings.data.colorSchemes.predefinedScheme);
    }
  }

  function generateFromWallpaper() {
    // Public function - uses debounce
    generate();
  }
  
  function doGenerateFromWallpaper(wpPath) {
    const wp = wpPath || WallpaperService.getWallpaper(Screen.name);
    if (!wp) {
      Logger.e("AppThemeService", "No wallpaper found");
      return;
    }
    
    const mode = Settings.data.colorSchemes.darkMode ? "dark" : "light";
    
    // If it's a video, use the thumbnail for color generation
    if (VideoWallpaperService.isVideoFile(wp)) {
      Logger.i("AppThemeService", "Wallpaper is video, generating colors from thumbnail");
      VideoWallpaperService.generateThumbnail(wp, function(thumbnailPath) {
        if (thumbnailPath) {
          Logger.i("AppThemeService", "Using thumbnail for matugen:", thumbnailPath);
          TemplateProcessor.processWallpaperColors(thumbnailPath, mode);
        } else {
          Logger.e("AppThemeService", "Failed to generate thumbnail for video wallpaper");
        }
      });
    } else {
      TemplateProcessor.processWallpaperColors(wp, mode);
    }
  }

  function generateFromPredefinedScheme(schemeData) {
    Logger.i("AppThemeService", "Generating templates from predefined color scheme");
    const mode = Settings.data.colorSchemes.darkMode ? "dark" : "light";
    TemplateProcessor.processPredefinedScheme(schemeData, mode);
  }
}
